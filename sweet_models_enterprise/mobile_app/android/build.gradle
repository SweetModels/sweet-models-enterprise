ext {
    flutter = [
        compileSdkVersion: 34,
        targetSdkVersion : 34,
        minSdkVersion    : 21
    ]

    // Align Kotlin across all subprojects to avoid Kotlin metadata incompatibility messages
    kotlin_version = '2.2.0'
}

// Some Flutter plugins still use legacy `buildscript { classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version" }`.
// Force them to align with the app's Kotlin toolchain.
subprojects {
    buildscript {
        configurations.classpath {
            resolutionStrategy.eachDependency { details ->
                if (details.requested.group == 'org.jetbrains.kotlin' && details.requested.name == 'kotlin-gradle-plugin') {
                    details.useVersion rootProject.ext.kotlin_version
                }
            }
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = "../build"
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(":app")
}

// Fix namespace requirement for AGP 8+ in flutter_secure_storage
subprojects { proj ->
    if (proj.name == "flutter_secure_storage") {
        proj.plugins.withId("com.android.library") {
            proj.android.namespace = "com.it_nomads.fluttersecurestorage"
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
